#!/usr/bin/env expect
#
# Automatic multi-factor authentication. Use `totp config list` to find secret names.
#
# Example:
#   auto_mfa -n <secret-name> -- <command>

set options {
  {n.arg "" "TOTP secret name. Use `totp config list` to find"}
}
set usage "- Automatic multi-factor authentication"

source ~/.dotfiles/scripts/expect/cmdline.tcl

set secret_name $params(n)
set program [lindex $argv 0]
set arguments [lrange $argv 1 end]
spawn $program {*}$arguments

proc process_auth {} {
  global secret_name
  set value [exec totp $secret_name]
  send "$value\n"
}

set auth_pattern "^.+:\[\[:space:]]*$"

expect -re $auth_pattern
process_auth

set success 0
set retry_count 1
while { $retry_count <= 3 } {
  expect {
    -re $auth_pattern { process_auth }
    -re "\[Pp]lease enter" { exp_continue }
    -re "\n\[\[:space:]]*\[a-zA-Z0-9]+" { set success 1; break }
    eof { exit }
  }
  set retry_count [expr $retry_count+1]
  sleep 0.5
}

if { $success == 1 } {
  if {$params(c) != ""} {
    eval $params(c)
  }
  interact
} else {
  puts "Authentication failed"
}
